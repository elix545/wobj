SUBROUTINE WOBJ.OBJECT.TOSTRING(JSON,POS)
*
INCLUDE WOBJ.INCLUDE
*
JSON:="{"; * START OUR STRING
NUMBER.PARTS=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,POS>
IF WOBJ.DEBUG THEN PRINT "POS=":POS:" NUMBER.PARTS=":NUMBER.PARTS
FOR P=1 TO NUMBER.PARTS
	PART.TYPE=JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,POS,P>
	PART.KEY=JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,POS,P>
	IF WOBJ.DEBUG THEN PRINT "POS=":POS:" P=":P:" T=":PART.TYPE:" K=":PART.KEY
	CALL WOBJ.ENCODE(PART.KEY,OUT.PART.KEY)
	JSON:='"':OUT.PART.KEY:'":'
	BEGIN CASE
		CASE PART.TYPE="S"
			CALL WOBJ.STRING.TOSTRING(JSON,POS,P)
		CASE PART.TYPE="B"
			CALL WOBJ.BOOLEAN.TOSTRING(JSON,POS,P)
		CASE PART.TYPE="D"
			CALL WOBJ.NUMBER.TOSTRING(JSON,POS,P)
		CASE PART.TYPE="N"
			CALL WOBJ.NULL.TOSTRING(JSON,POS,P)
		CASE PART.TYPE="O"
			PART.FLINK=JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,POS,P>
			IF WOBJ.DEBUG THEN PRINT "OBJ FLINK=":PART.FLINK
			CALL WOBJ.OBJECT.TOSTRING(JSON,PART.FLINK)
		CASE PART.TYPE="A"
			PART.FLINK=JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,POS,P>
			IF WOBJ.DEBUG THEN PRINT "FL=":PART.FLINK
			CALL WOBJ.ARRAY.TOSTRING(JSON,PART.FLINK)
	END CASE
	IF P <> NUMBER.PARTS THEN
		JSON:=", "
	END
NEXT P
*
JSON:="}"
RETURN
