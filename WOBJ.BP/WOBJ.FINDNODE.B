SUBROUTINE WOBJ.FINDNODE(INNODE,NODEPOS,PARTPOS,PARTVALUE,LASTNODE,RERR)
*
INCLUDE WOBJ.INCLUDE
*
* NEED TO BREAK OUR NODE REQUEST UP INTO IT'S SEPERATE PARTS
*
PARTVALUE=""
RERR=""
LASTNODE=""
*
OBJECT.SEPERATOR = "."
NODES=INNODE
CONVERT OBJECT.SEPERATOR TO CHAR(254) IN NODES
*
NODEPOS=1; * WE ALWAYS START WITH THE FIRST ONE.
*
NUMBER.NODES=DCOUNT(NODES,@AM)
* We need to trick this into seeing an array as another node level
ORIG.NODES=NODES
NODES=""
FOR A=1 TO NUMBER.NODES
    NODE=ORIG.NODES<A>
    IF INDEX(NODE,"[",1) THEN
        ACTUAL.NODE=FIELD(NODE,"[",1)
        NODES<-1>=ACTUAL.NODE
        NODES<-1>=NODE
    END ELSE
        NODES<-1>=NODE
    END
NEXT A
IF WOBJ.DEBUG THEN
    PRINT "FINDNODE: NODES: ":NODES
END
NUMBER.NODES=DCOUNT(NODES,@AM)
IF WOBJ.DEBUG THEN PRINT "NUMBER NODES: ":NUMBER.NODES:" ":NODES
FOR A=1 TO NUMBER.NODES
	NODE=NODES<A>
    LASTNODE=NODE
    NODETYPE=JSONOBJECT(JSONOBJECT$NODETYPE)<1,NODEPOS>
	IF WOBJ.DEBUG THEN PRINT "NODE ":A:" NODE=":NODE:" NODEPOS=":NODEPOS:" NODETYPE=":NODETYPE
    BEGIN CASE
    	CASE NODETYPE="O"
            JUSTNODE=FIELD(NODE,"[",1)
            IF WOBJ.DEBUG THEN
                PRINT "FINDNODE: Locating ":JUSTNODE
                PRINT "                in ":JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,NODEPOS>
            END
   		    LOCATE JUSTNODE IN JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,NODEPOS> SETTING PARTPOS ELSE PARTPOS=0
        CASE NODETYPE="A"
            PARTPOS=FIELD(NODE,"[",2)
            PARTPOS=FIELD(PARTPOS,"]",1)
            PARTPOS+=1
            IF PARTPOS > JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS> THEN
               IF WOBJ.DEBUG THEN PRINT "WOBJ.FINDNODE: PARTPOS=":PARTPOS:" LEN=":JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS>:" NODEPOS=":NODEPOS
               PARTPOS=0
            END
    END CASE
    IF WOBJ.DEBUG THEN PRINT "Partpos=":PARTPOS
    IF NOT(PARTPOS) THEN
            RERR=1
            RERR<2>="NODE: ":NODE:" NOT FOUND AT LEVEL ":NODEPOS
            IF WOBJ.DEBUG THEN PRINT RERR<2>
            RETURN
    END
	PARTVALUE=JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEPOS,PARTPOS>
    PART.TYPE=JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>
    PART.FLINK=JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,NODEPOS,PARTPOS>
    PART.ENCODING=JSONOBJECT(JSONOBJECT$NODEPARTS.ENCODING)<1,NODEPOS,PARTPOS>
    IF WOBJ.DEBUG THEN
        PRINT "FINDNODE: PART.TYPE:":PART.TYPE:" PART.FLINK:":PART.FLINK
    END
    IF (PART.TYPE = "A" OR PART.TYPE="O") AND PART.FLINK # "" THEN
       * ADJUST OUR NODEPOS TO OUR NEW OBJECT
       IF WOBJ.DEBUG THEN PRINT "MOVING TYPE: ":PART.TYPE:" MOVING TO OBJECT ":PART.FLINK
       NODEPOS=PART.FLINK
    END ELSE
	*  NEED TO DO DECONDING
	    IF WOBJ.DEBUG THEN PRINT "DECODING: PARTPOS=":PARTPOS:" VALUE=":PARTVALUE
        BEGIN CASE
            CASE PART.ENCODING="H"
                PARTVALUE=OCONV(PARTVALUE,"MY")
            CASE PART.ENCODING="A"
        END CASE
    END
NEXT A

RETURN
