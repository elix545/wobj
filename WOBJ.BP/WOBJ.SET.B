SUBROUTINE WOBJ.SET(ACTION,NODE,VALUE,RERR)
*
INCLUDE WOBJ.INCLUDE
*
IF WOBJ.DEBUG THEN PRINT "WOBJ.SET: NODE=":NODE
CALL WOBJ.FINDNODE(NODE,NODEPOS,PARTPOS,PARTVALUE,LASTNODE,RERR)
IF WOBJ.DEBUG THEN PRINT "WOBJ.SET: NODEPOS=":NODEPOS:" PARTPOS=":PARTPOS:" LASTNODE=":LASTNODE:" RERROR=":RERR<2>
*
IF NODE AND NOT(NODEPOS) THEN
    RERR=-1
    RERR<2>="Could not find node"
END
NODETYPE = JSONOBJECT(JSONOBJECT$NODETYPE)<1,NODEPOS>
IF NOT(PARTPOS) THEN
   * WE NEED TO ADD A NEW ELEMENT
   IF NODETYPE="O" THEN
        PARTPOS=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS>
        PARTPOS+=1
        JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS>=PARTPOS
   		JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,NODEPOS,PARTPOS>=LASTNODE
   END ELSE
   		PARTPOS=FIELD(LASTNODE,"[",2)
   		PARTPOS=FIELD(PARTPOS,"]",1)
   		IF PARTPOS<1 THEN
   			PARTPOS=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS>
        	PARTPOS+=1
        	JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS>=PARTPOS
   		END ELSE
   		    PARTPOS+=1
   			LASTPARTPOS=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS>+1
   			FOR P=LASTPARTPOS TO PARTPOS
   				JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,NODEPOS,P>=""
   				JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,P>="S"
   				JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,NODEPOS,P>=""
   				JSONOBJECT(JSONOBJECT$NODEPARTS.BLINK)<1,NODEPOS,P>=""
   				JSONOBJECT(JSONOBJECT$NODEPARTS.ENCODING)<1,NODEPOS,P>=""
   				JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEPOS,P>=""
   			NEXT P
   			JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEPOS>=PARTPOS
   		END
   END
END
PARTTYPE=JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>
IF WOBJ.DEBUG THEN PRINT "WOBJ.SET: PARTTYPE=":PARTTYPE
SACTION=FIELD(ACTION,".",2)
BEGIN CASE
	CASE SACTION="ARRAY" OR SACTION="OBJECT"
		* THIS IS GOING TO INSERT A NEW LINK. YOU WILL LOOSE YOUR OLD STUFF
		* FUTURE VERSION NEEDS TO CLEAN OUT THE NON-USED STUFF
		NODECNTR=JSONOBJECT(JSONOBJECT$NODECNTR)
		NODECNTR+=1; * OUR NEW NODE POSITION
		JSONOBJECT(JSONOBJECT$NODECNTR)=NODECNTR
		JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>=SACTION[1,1]
		JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,NODEPOS,PARTPOS>=NODECNTR
		JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEPOS,PARTPOS>=""
		* NOW LETS SETUP OUR NEW OBJECT
		JSONOBJECT(JSONOBJECT$NODETYPE)<1,NODECNTR>=SACTION[1,1]
		JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODECNTR>=0
	CASE SACTION="NULL"
		JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEPOS,PARTPOS>=VALUE
		JSONOBJECT(JSONOBJECT$NODEPARTS.ENCODING)<1,NODEPOS,PARTPOS>=""
		JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>="N"
	CASE SACTION="NUMBER"
		JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEPOS,PARTPOS>=VALUE
		JSONOBJECT(JSONOBJECT$NODEPARTS.ENCODING)<1,NODEPOS,PARTPOS>=""
		JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>="D"
	CASE SACTION="BOOLEAN"
  		JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEPOS,PARTPOS>=VALUE
		JSONOBJECT(JSONOBJECT$NODEPARTS.ENCODING)<1,NODEPOS,PARTPOS>=""
		JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>="B"
	CASE 1
	    IF INDEX(VALUE,CHAR(13),1) OR INDEX(VALUE,CHAR(10),1) OR INDEX(VALUE,CHAR(254),1) OR INDEX(VALUE,CHAR(253),1) OR INDEX(VALUE,CHAR(252),1) THEN
	    	V=OCONV(VALUE,"MX")
	    	E="H"
	    END ELSE E="A"
		JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEPOS,PARTPOS>=VALUE
		JSONOBJECT(JSONOBJECT$NODEPARTS.ENCODING)<1,NODEPOS,PARTPOS>=E
		JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>="S"
END CASE
RETURN
